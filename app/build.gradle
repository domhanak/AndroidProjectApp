// versioning
def versionMajor = 1
def versionMinor = 1
def versionPatch = 0
def versionBuild = 1

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent,
                    "$project.name-$variant.name-$variant.versionName-${variant.versionCode}.apk")
        }
    }

    // app signing
    // constants below are saved in gradle.properties
    signingConfigs {
        paidRelease {
            keyAlias 'release-key'
            keyPassword '5568942'
            storeFile file('C:/Users/User/AndroidDev/AndroidProjectApp/release-key.jks')
            storePassword '5568942'
        }
    }

    defaultConfig {
        applicationId "cz.muni.fi.pv256.movio.uco410430"
        minSdkVersion 15
        targetSdkVersion 23

        resConfigs "cs", "en" // only czech and english langs are supported
        resConfigs "hdpi", "xhdpi", "xxhdpi", "xxxhdpi"

        versionCode versionMajor * 10000 + versionMinor * 1000 + versionPatch * 100 + versionBuild
        versionName "${versionMajor}.${versionMinor}.${versionPatch}.${versionBuild}"

        buildConfigField "String", "tmd_api_key", TMD_API_KEY
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.paidRelease
        }
        debug {
            applicationIdSuffix '.debug'
        }
    }
    productFlavors {
        paid {
            applicationId "cz.muni.fi.pv256.movio.uco410430.paid"
            buildConfigField "boolean", "LOGGING_ENABLED", "true"
        }
        free {
            applicationId "cz.muni.fi.pv256.movio.uco410430.free"
            buildConfigField "boolean", "LOGGING_ENABLED", "false"
        }
        flavor {
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.getbase:floatingactionbutton:1.9.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.google.code.gson:gson:2.4'
    compile 'de.greenrobot:eventbus:2.4.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    compile 'com.github.bumptech.glide:okhttp-integration:1.3.1'
    compile files('libs/stickygridheaders.jar')
}
